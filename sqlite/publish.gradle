apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))

bintray {
    user = keystoreProperties['BINTRAY_USER']
    key = keystoreProperties['BINTRAY_KEY']

    configurations = ['archives']
    publications = ['aar']
    override = true

    pkg {
        repo = 'android'
        name = 'sqlite-common'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/diogo0liveira/Laboratory.git'
        websiteUrl = 'https://github.com/diogo0liveira/Laboratory'
        issueTrackerUrl = 'https://github.com/diogo0liveira/Laboratory/issues'

        version {
            name = '1.0.0'
            vcsTag = android.defaultConfig.versionName
            released = new Date()
        }

        publish = true
        publicDownloadNumbers = true
    }
}

publishing {
    publications {
        aar(MavenPublication) {
            version '1.0.0'
            artifactId 'sqlite'
            groupId 'com.dao.mobile.artifact'
            artifact("$buildDir/outputs/aar/${artifactId}-release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}